apply plugin: 'java'
apply plugin: 'clojure'
apply plugin: 'eclipse'
apply plugin: 'idea'
apply plugin: 'osgi'

dependencies {
    compile project(':rxjava-core')
    provided 'org.clojure:clojure:1.4.+'
    provided 'junit:junit-dep:4.10'
    provided 'org.mockito:mockito-core:1.8.5'
    
    // clojure
    testCompile 'clj-http:clj-http:0.6.4' // https://clojars.org/clj-http
}

/*
 * Clojure
 */
aotCompile = true
warnOnReflection = true

buildscript {
  repositories { maven { url "http://clojars.org/repo" } }
  dependencies { classpath "clojuresque:clojuresque:1.5.4" }
}

repositories {
  mavenCentral()
  clojarsRepo()
}

/*
 * Add Counterclockwise and include 'provided' dependencies
 */
eclipse {
  project {
    natures "ccw.nature"
  }
  classpath {
    plusConfigurations += configurations.provided
    downloadSources = true
    downloadJavadoc = true
  }
}

idea {
	module {
		// include 'provided' dependencies on the classpath
		scopes.PROVIDED.plus += configurations.provided
	}
}


// include /src/examples folder
sourceSets {
    examples
}

// make 'examples' use the same classpath
configurations {
    examplesCompile.extendsFrom compile
    examplesRuntime.extendsFrom runtime
}

// include 'examples' in build task
build.dependsOn examplesClasses

jar {
    manifest {
        name = 'rxjava-clojure'
        instruction 'Bundle-Vendor', 'Netflix'
        instruction 'Bundle-DocURL', 'https://github.com/Netflix/RxJava'
        instruction 'Import-Package', '!org.junit,!junit.framework,!org.mockito.*,*'
        instruction 'Fragment-Host', 'com.netflix.rxjava.core'
    }
}