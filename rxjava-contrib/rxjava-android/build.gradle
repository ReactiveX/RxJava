apply plugin: 'java'
apply plugin: 'eclipse'
apply plugin: 'idea'
apply plugin: 'osgi'

sourceCompatibility = JavaVersion.VERSION_1_6
targetCompatibility = JavaVersion.VERSION_1_6

dependencies {
    compile project(':rxjava-core')
    provided 'junit:junit-dep:4.10'
    provided 'org.mockito:mockito-core:1.8.5'
    provided 'com.pivotallabs:robolectric:1.2'
    provided 'com.google.android:android:4.0.1.2'
}

eclipse {
  classpath {
    // include 'provided' dependencies on the classpath
    plusConfigurations += configurations.provided

    downloadSources = true
    downloadJavadoc = true
  }
}

idea {
	module {
		// include 'provided' dependencies on the classpath
		scopes.PROVIDED.plus += configurations.provided
	}
}

javadoc {
    options {
        doclet = "org.benjchristensen.doclet.DocletExclude"
        docletpath = [rootProject.file('./gradle/doclet-exclude.jar')]
        stylesheetFile = rootProject.file('./gradle/javadocStyleSheet.css')
        windowTitle = "RxJava Android Javadoc ${project.version}"
    }
    options.addStringOption('top').value = '<h2 class="title" style="padding-top:40px">RxJava Android</h2>'
}

jar {
    exclude "rx/android/testsupport"

    manifest {
        name = 'rxjava-android'
        instruction 'Bundle-Vendor', 'Netflix'
        instruction 'Bundle-DocURL', 'https://github.com/Netflix/RxJava'
        instruction 'Import-Package', '!org.junit,!junit.framework,!org.mockito.*,*'
    }
}

test {
    testLogging {
        exceptionFormat "full"
        events "started"
        displayGranularity 2
    }
}
